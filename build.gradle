plugins {
    id 'io.freefair.lombok' version "9.0.0" apply false
    id 'com.adarshr.test-logger' version "4.0.0" apply false
    id 'org.springframework.boot' version "3.5.7" apply false
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'java-test-fixtures'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    group = 'eu.fraho.spring'
    version = rootProject.file('version.txt').text.trim()
    description = "Spring boot security integration for JWT"

    java {
        withJavadocJar()
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-parameters'
        options.compilerArgs << '-Xlint:deprecation'
    }

    javadoc {
        options.addBooleanOption('Xdoclint:none', true)
    }

    jar {
        manifest {
            attributes(
                    'Implementation-Title': rootProject.name,
                    'Implementation-Version': project.version,
                    'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                    'Compatibility': project.java.sourceCompatibility,
                    'Built-By': System.getProperty('user.name')
            )
        }
    }

    test {
        useJUnitPlatform()
        ignoreFailures = rootProject.version.endsWith('-SNAPSHOT')
    }

    dependencies {
        annotationProcessor group: "org.springframework.boot", name: "spring-boot-configuration-processor", version: "3.5.7"

        testFixturesApi group: "org.springframework.boot", name: "spring-boot-starter-test", version: "3.0.0"
        testFixturesApi group: "org.springframework.boot", name: "spring-boot-starter-web", version: "3.0.0"
        testFixturesApi group: "com.fasterxml.jackson.datatype", name: "jackson-datatype-jsr310", version: "2.20.1"
        testFixturesApi platform("org.junit:junit-bom:6.0.0")
        testFixturesApi "org.junit.jupiter:junit-jupiter-api"
        testFixturesApi "org.junit.jupiter:junit-jupiter-params"
        testRuntimeOnly "org.junit.platform:junit-platform-launcher"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    }

    configurations.configureEach {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    // code coverage
    apply plugin: 'jacoco'
    jacoco.toolVersion = "0.8.14"
    jacocoTestReport.reports {
        xml.required = true
        html.required = true
    }
    check.dependsOn(jacocoTestReport)

    // release to central
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = project.group
                artifactId = "${rootProject.name}-${project.name}"
                version = project.version
                from components.java

                suppressPomMetadataWarningsFor('testFixturesApiElements')
                suppressPomMetadataWarningsFor('testFixturesRuntimeElements')

                pom {
                    name = "${rootProject.name}-${project.name}"
                    description = project.description
                    url = 'https://github.com/bratkartoffel/security-jwt'

                    scm {
                        connection = 'scm:git:https://github.com/bratkartoffel/security-jwt.git'
                        developerConnection = 'scm:git:git@github.com:bratkartoffel/security-jwt.git'
                        url = 'https://github.com/bratkartoffel/security-jwt.git'
                    }

                    licenses {
                        license {
                            name = 'The MIT License (MIT)'
                            url = 'https://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'bratkartoffel'
                            name = 'Simon Frankenberger'
                            email = 'simon-ossrh-release@fraho.eu'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : null
                    password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : null
                }
                url = project.version.endsWith('-SNAPSHOT') ?
                        'https://oss.sonatype.org/content/repositories/snapshots' :
                        'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            }
        }
    }

    // Signature of artifacts
    if (project.hasProperty('signing.gnupg.keyName')) {
        signing {
            useGpgCmd()
            required = !rootProject.version.endsWith('-SNAPSHOT')
            sign publishing.publications.mavenJava
        }
    } else {
        logger.info("Disable signing of artifacts as no key is configured")
    }
}

tasks.register('jacocoRootReport', JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs.from(
            subprojects.sourceSets.main.allSource.srcDirs,
            subprojects.sourceSets.main.output,
            subprojects.jacocoTestReport.executionData
    )
    reports {
        html.required = true
        xml.required = true
    }
    onlyIf = {
        true
    }
}
