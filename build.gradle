buildscript {
    ext {
        projectVersion = '4.0.0'

        springBootVersion = '2.0.5.RELEASE'
        springDependencyManagementVersion = '1.0.6.RELEASE'

        lombokVersion = '1.18.2'
        swaggerAnnotations2Version = '1.5.21'
        swaggerAnnotations3Version = '2.0.5'
        nimbusJwtVersion = '6.0.2'
        commonsCodecVersion = '1.11'
        bouncyCastleVersion = '1.60'
        spyMemcachedVersion = '2.12.3'
        expiringMapVersion = '0.5.8'
        h2Version = '1.4.197'
        powerMockVersion = '1.7.4'
        jedisVersion = '2.9.0'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0"
        classpath 'io.spring.gradle:propdeps-plugin:0.0.10.RELEASE'
        classpath "gradle.plugin.gradle-plugins:jartest:1.0.1"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    group = 'eu.fraho.spring'
    archivesBaseName = "security-jwt-${project.name}"
    version = "${projectVersion}"
    description = "Spring boot security integration for JWT"

    jar {
        version = "${project.version}"
        manifest {
            attributes(
                    "Implementation-Title": "Gradle",
                    "Implementation-Version": version
            )
        }
    }

    buildscript {
        repositories {
            mavenLocal()
            mavenCentral()
        }
    }

    configurations {
        apt
    }
    
    dependencies {
        compileOnly("org.projectlombok:lombok:${lombokVersion}")
        
        apt("org.projectlombok:lombok:${lombokVersion}")
        apt("org.springframework.boot:spring-boot-configuration-processor")

        testCompile('org.springframework.boot:spring-boot-starter-test')
        testCompile('org.springframework:spring-aspects')
        testCompile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")
    }

    // do not abort on test failures
    test {
        ignoreFailures = true
    }

    // propdeps
    compileJava {
        dependsOn(processResources)
        options.annotationProcessorPath = configurations.apt
    }

    // transitive dependencies for tests
    apply plugin: 'com.github.hauner.jarTest'

    // release stuff
    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task testsJar(type: Jar) {
        classifier = 'tests'
        from sourceSets.test.output
        from sourceSets.test.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar, testsJar
    }

    // code coverage
    apply plugin: 'jacoco'
    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }
    check.dependsOn jacocoTestReport

    // release to central
    apply plugin: 'signing'
    apply plugin: 'maven'
    if (project.hasProperty('release')) {
        // abort on test failures on releasing
        test {
            ignoreFailures = false
        }

        // Signature of artifacts
        signing {
            useGpgCmd()
            sign configurations.archives
        }

        // OSSRH publication
        uploadArchives {
            repositories {
                mavenDeployer {
                    // POM signature
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    // Target repository
                    if (project.version.endsWith('-SNAPSHOT')) {
                        repository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                            authentication(userName: ossrhUsername, password: ossrhPassword)
                        }
                    } else {
                        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                            authentication(userName: ossrhUsername, password: ossrhPassword)
                        }
                    }
                    pom.project {
                        name project.archivesBaseName
                        description project.description
                        url 'https://github.com/bratkartoffel/security-jwt'

                        scm {
                            connection 'scm:git:https://github.com/bratkartoffel/security-jwt.git'
                            developerConnection 'scm:git:git@github.com:bratkartoffel/security-jwt.git'
                            url 'https://github.com/bratkartoffel/security-jwt.git'
                        }

                        licenses {
                            license {
                                name 'The MIT License (MIT)'
                                url 'http://opensource.org/licenses/MIT'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = 'bratkartoffel'
                                name = 'Simon Frankenberger'
                                email = 'simon-ossrh-release@fraho.eu'
                            }
                        }
                    }
                }
            }
        }
    }
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output)
    executionData = files(subprojects.jacocoTestReport.executionData)
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
    onlyIf = {
        true
    }
    doFirst {
        executionData = files(executionData.findAll {
            it.exists()
        })
    }
}
